#
 # Copyright (C) 2010 ARM Limited. All rights reserved.
 
 # Permission is hereby granted, free of charge, to any person obtaining a copy
 # of this software and associated documentation files (the "Software"), to deal
 # in the Software without restriction, including without limitation the rights
 # to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 # copies of the Software, and to permit persons to whom the Software is
 # furnished to do so, subject to the following conditions:
 
 # The above copyright notice and this permission notice shall be included in
 # all copies or substantial portions of the Software.
 
 # THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 # IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 # FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 # AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 # LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 # OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 # THE SOFTWARE.
 # /

import os
Import('env')

# Generate a build environment for the integration tests, taking a copy of the top-level build environment 
# (env) as a start. This environment is then passed to the varius unit test's
# sconscripts:

x11_env = env.Clone()
x11_env.Append( CPPPATH = ['#x11/thirdparty/include',
                           '#x11/thirdparty/include/xorg',
                           '#x11/thirdparty/include/pixman-1',
                           '#x11/thirdparty/include/libdrm',
                           '#x11/mali_drv'
                         ],
                CPPDEFINES = {'USE_TRACING': '1'}
              )

# Xorg uses C++ style comments and 'inline' keyword
if '-std=c89' in x11_env['CFLAGS']:
	x11_env['CFLAGS'].remove('-std=c89')

if '-pedantic' in x11_env['CCFLAGS']:
	x11_env['CCFLAGS'].remove('-pedantic')

# X11 generates a lot of warnings
if '-Werror' in x11_env['CCFLAGS']:
	x11_env['CCFLAGS'].remove('-Werror')

# We want to keep undefined references (we can't link against Xorg)
x11_env['_LIBFLAGS'] = x11_env['_LIBFLAGS'].replace('-Wl,--no-undefined ','')

#remove the 'lib'prefix
x11_env['LIBPREFIX'] = ''

objs = x11_env.SharedObject(Glob('*.c'))
objs = SConscript('src/sconscript', exports='x11_env')

x11_shared_lib = x11_env.SharedLibrary(target  = '$SHARED_LIB_PATH/mali_drv',
                                   source  = objs,
                                   LIBS    = ['pthread', 'mali'],
                                   RPATH   = ['$SHARED_LIB_PATH'],
                                   LIBPATH = ['$SHARED_LIB_PATH'])

env.LibTarget('x11', x11_shared_lib)
